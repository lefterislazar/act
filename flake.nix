{
  description = "act";

  inputs = {
    flake-utils.url = "github:numtide/flake-utils";
    nixpkgs.url = "github:nixos/nixpkgs/nixpkgs-unstable";
    nixpkgs_rocq.url = "github:nixos/nixpkgs/nixpkgs-unstable";
    hevm = {
      url = "github:lefterislazar/hevm/unknown-code";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs = { nixpkgs, nixpkgs_rocq, flake-utils, hevm, ... }:
    flake-utils.lib.eachDefaultSystem (system:
      let
        pkgs = nixpkgs.legacyPackages.${system};
        pkgs_rocq = nixpkgs_rocq.legacyPackages.${system};
        gitignore = pkgs.nix-gitignore.gitignoreSourcePure [ ./.gitignore ];
        hspkgs = pkgs.haskellPackages.override {
          overrides = self: super: {
            hevm = hevm.packages.${system}.unwrapped;
          };
        };
        act = (hspkgs.callCabal2nixWithOptions "act" (gitignore ./.) "-fci" {})
          .overrideAttrs (attrs : {
            buildInputs = attrs.buildInputs ++ [
              pkgs.z3
              pkgs.cvc5
              pkgs.solc
              pkgs.vyper
              pkgs.bitwuzla
            ];
          });
      in rec {
        packages.act = act;
        packages.default = packages.act;

        apps.act = flake-utils.lib.mkApp { drv = packages.act; };
        apps.default = apps.act;

        devShell = let
          libraryPath = "${pkgs.lib.makeLibraryPath (with pkgs; [ libff secp256k1 gmp ])}";
        in hspkgs.shellFor {
          packages = _: [ act ];
          buildInputs = [
            hspkgs.cabal-install
            hspkgs.haskell-language-server
            pkgs.jq
            pkgs.z3
            pkgs.cvc5
            pkgs_rocq.rocq-core
            pkgs_rocq.rocqPackages.stdlib
            pkgs_rocq.rocqPackages.vsrocq-language-server
            pkgs.solc
            pkgs.mdbook
            pkgs.mdbook-mermaid
            pkgs.mdbook-katex
            pkgs.secp256k1
            pkgs.libff
            pkgs.vyper
            pkgs.bitwuzla
          ];
          withHoogle = true;
          shellHook = ''
            export PATH=$(pwd)/bin:$PATH
            export DYLD_LIBRARY_PATH="${libraryPath}"
          '';
        };
      }
    );
}
