{-# Language GADTs #-}
{-# Language RankNTypes #-}
{-# Language ScopedTypeVariables #-}
{-# Language NamedFieldPuns #-}
{-# Language DataKinds #-}
{-# Language KindSignatures #-}
{-# Language ApplicativeDo #-}
{-# Language ViewPatterns #-}
{-# Language TypeOperators #-}
{-# Language InstanceSigs #-}
{-# Language TupleSections #-}

module Act.Type (typecheck, Err, ErrSrc, Constraint(..), Env(..), Constructors, addCalldata, addPreconds, constraintSource, emptyEnv, addIffs, hasSign, errorSource) where

import Prelude hiding (GT, LT)
import Data.Maybe (isJust, fromMaybe)
import Data.Map.Strict    (Map)
import Data.Bifunctor (first)
import qualified Data.Map.Strict    as Map
import Data.Typeable ((:~:)(Refl))
import Data.Type.Equality (TestEquality(..))
import Control.Monad (unless)
import Data.Foldable

import Act.Syntax
import Act.Syntax.Timing
import Act.Syntax.Untyped qualified as U
import Act.Syntax.TypedImplicit
import Act.Error
import Act.Print

type Err = Error String
-- Error including source path
type ErrSrc = Error (FilePath, String)

-- | Enrich errors with source information
errorSource :: String -> Err a -> ErrSrc a
errorSource _ (Success a) = Success a
errorSource source (Failure es) = Failure (fmap (\(p,e) -> (p,(source, e))) es)

-- | A map containing the definitions off all constructors
type Constructors = Map Id Constructor

-- | A map containing the definitions off all constructors
type Transitions = Map Id (Map Id Behaviour)

-- | The type checking environment.
data Env = Env
  { contract     :: Id                           -- ^ The name of the current contract.
  , storage      :: StorageTyping                -- ^ StorageTyping
  , calldata     :: Map Id ArgType               -- ^ The calldata var names and their types.
  , retdata      :: Map Id ArgType               -- ^ The calldata var names and their types.
  , constructors :: Constructors                 -- ^ Interfaces and preconditions of constructors
  , transitions  :: Transitions                 -- ^ Interfaces and preconditions of transitions
  , preconds     :: [Exp ABoolean Untimed]       -- ^ Constraint context
  , numInters    :: Int                          -- ^ Number of interactions
  }
  deriving (Show, Eq)

emptyEnv :: Env
emptyEnv = Env
  { contract     = ""
  , storage      = mempty
  , calldata     = mempty
  , retdata      = mempty
  , constructors = mempty
  , transitions  = mempty
  , preconds     = []
  , numInters    = 0
  }

-- | Functions to manipulate environments

-- Add the name of the current contract to the environment
addContractName :: Id -> Env -> Env
addContractName cid env = env{ contract = cid }

-- Add calldata arguments of the current constructor/transition to the environment
addCalldata :: [Arg] -> Env -> Env
addCalldata decls env = env{ calldata = abiVars }
  where
   abiVars = Map.fromList $ map (\(Arg typ var) -> (var, typ)) decls

-- Add retdata arguments of the current constructor/transition to the environment
addRetdata :: [Arg] -> Env -> Err Env
addRetdata rets env@Env{calldata} =
  foldValidation (\m (Arg typ var) ->
    case Map.lookup var m of
      Just _ -> throw (nowhere, "Variable " <> var <> " is already defined")
      Nothing -> pure (Map.insert var typ m)
  ) calldata rets `bindValidation` \newCall ->
  pure env{ calldata = newCall }

-- Add storage typing of a contract to the environment
addConstrStorage :: Id -> Map Id (ValueType, Integer) -> Env -> Env
addConstrStorage cid storageTyping env =
    env { storage = Map.insert cid storageTyping (storage env) }

-- Add the whole constructor to the environment. This is needed to typecheck
-- constructor calls and also later on, to perform the entailment checking.
addConstructor :: Id -> Constructor -> Env -> Env
addConstructor cid constr env =
  env { constructors = Map.insert cid constr (constructors env)  }

addTransition :: Id -> Behaviour -> Env -> Env
addTransition cid trans env =
  env { transitions = Map.insert cid (Map.insert (_name trans) trans (fromMaybe Map.empty $ Map.lookup cid $ transitions env)) (transitions env) }

-- Add constructor preconditions to the environment
addPreconds :: [Exp ABoolean Untimed] -> Env -> Env
addPreconds pres env =
  env { preconds = pres <> preconds env }

-- Increment the interactions counter
incrInters :: Env -> Env
incrInters env = env { numInters = numInters env + 1 }

-- Clear local environment (calldata and preconditions)
clearLocalEnv :: Env -> Env
clearLocalEnv env =
  env { calldata = mempty, preconds = mempty }

-- | Constraints generated during type checking.
-- An integer constraint constrains an integer expression to fit within the bounds of a given type.
-- A call constraint constrains the arguments of a constructor call to satisfy the constructor's preconditions.
data Constraint t =
    BoolCnstr Pn FilePath String Env (Exp ABoolean t)                          -- ^ Boolean constraint with a message, environment, and boolean expression. Generated to check integer bounds, case consistency, and array bounds.
  | CallCnstr Pn FilePath String Env [TypedExp t] (Maybe (Exp AInteger t)) Id  -- ^ Call constraint with a message, environment, argument list, callvalue, and constructor id. Generated to check that preconditions of the called constructor are satisfied.
    deriving (Show, Eq)

instance Annotatable Constraint where
  annotate :: Constraint Untimed -> Constraint Timed
  annotate (BoolCnstr p src msg env e) = BoolCnstr p src msg env (setPre e)
  annotate (CallCnstr p src msg env es cv i) = CallCnstr p src msg env (setPre <$> es) (setPre <$> cv) i

constraintSource :: FilePath -> Constraint t -> Constraint t
constraintSource src (BoolCnstr p _ str env e) = BoolCnstr p src str env e
constraintSource src (CallCnstr p _ str env ts me e) = CallCnstr p src str env ts me e

-- | Create an integer bound constraint
makeIntegerBoundConstraint :: Pn -> String -> Env -> TValueType AInteger -> Exp AInteger t -> Constraint t
makeIntegerBoundConstraint p str env t e = BoolCnstr p "" str env (InRange nowhere t e)

-- | Create an array bound constraint
makeArrayBoundConstraint :: Pn -> String -> Env -> Int -> Exp AInteger t -> Constraint t
makeArrayBoundConstraint p str env len e = BoolCnstr p "" str env (LT p e (LitInt p (fromIntegral len)))

-- | Top-level typechecking function
typecheck :: [(U.Act, String)] -> ErrSrc (Act, [Constraint Timed])
typecheck acts =
    (\(storageTyping, tcontracts, cnstrs) -> (Act storageTyping tcontracts, annotate <$> cnstrs)) <$> checkContracts' allContracts
    where
        mergeContracts (U.Main contracts, source) = (,source) <$> contracts
        allContracts = concatMap mergeContracts acts

checkContracts' :: [(U.Contract, FilePath)] -> ErrSrc (StorageTyping, [Contract], [Constraint Untimed])
checkContracts' cs = (\(s, tcs, cnstrs) -> (storage s, tcs, cnstrs)) <$> checkContracts emptyEnv cs

-- | Typecheck a list of contracts
checkContracts :: Env -> [(U.Contract, FilePath)] -> ErrSrc (Env, [Contract], [Constraint Untimed])
checkContracts env [] = pure (env, [], [])
checkContracts env ((U.Contract p cid decls cnstr behvs, source):cs) =
    -- Check that the constructor name is not already defined
    errSrc (checkContrName cid env) *>
    -- Add contract name to environment
    let env' = addContractName cid env in
    (errSrc $ makeStorageTypingFromDecls env decls) `bindValidation` \storageType ->
    -- Check constructor
    (errSrc $ checkConstructor env' storageType cnstr) `bindValidation` \(constr', env'', cnstrs1) ->
    -- Check behaviors
    (errSrc $ checkBehaviours env'' behvs) `bindValidation` \(behvs', env''', cnstrs2) ->
      -- Check remaining contracts
    checkContracts env''' cs `bindValidation` \(env'''', cs', cnstrs3) ->
    pure (env'''', (Contract source constr' behvs') : cs', (cnstrSrc <$> cnstrs1 ++ cnstrs2) ++ cnstrs3)
    where
        checkContrName :: Id -> Env -> Err ()
        checkContrName cid' Env{constructors} =
            case Map.lookup cid' constructors of
                Just _ -> throw (p, "Constructor " <> cid' <> " is already defined")
                Nothing -> pure ()
        errSrc :: Err a -> ErrSrc a
        errSrc = errorSource source
        cnstrSrc = constraintSource source

-- | Derive storage typing from a list of storage variable declarations
makeStorageTypingFromDecls :: Env -> [U.StorageVar] -> Err (Map Id (ValueType, Integer))
makeStorageTypingFromDecls env decls = Map.insert "BALANCE" (ValueType $ TInteger 256 Unsigned, -1) . Map.fromList <$> go decls 0
  where
    go :: [U.StorageVar] -> Integer -> Err [(Id,(ValueType, Integer))]
    go [] _ = pure []
    go (U.StorageVar p typ@(ValueType vt) name : rest) slot
      | name == "BALANCE" = throw (p, "BALANCE identifier is reserved")
      | otherwise         = validSlotType env p vt *> fmap (((:)) (name, (typ, slot))) (go rest (slot+1))

-- | Typecheck a constructor
checkConstructor :: Env -> Map Id (ValueType, Integer) -> U.Constructor -> Err (Constructor, Env, [Constraint Untimed])
checkConstructor env storageType (U.Constructor p (Interface p' params) payable (U.Block iffs cases) posts _) =
    let cid = contract env
        env' = addCalldata params env
        env'' = addConstrStorage cid storageType env' in
    -- check that parameter types are valid
    traverse_ (checkParams env'') params *>
    -- check preconditions and add implicit CALLVALUE == 0 precondition if not payable
    -- checkPreconditions env' (addCallvalueZeroPrecond payable iffs) `bindValidation` \(iffs', cnstr1, env'') ->
    checkBlock env'' Nothing (U.Block (addCallvalueZeroPrecond payable iffs) cases) `bindValidation` \(block', cnstr1, env''') ->
    -- add storage typing to environment so cases can reference storage vars
    -- check if payable constructor has BALANCE declared
    (if payable == Payable then balanaceDeclared p storageType else pure ()) *>
    do-- check cases
    -- (unzip <$> traverse (checkCase env''' Nothing) (adjustCasePosn p cases)) `bindValidation` \(cases', cnstr2) -> do
    -- check case consistency
    -- let casecnstrs = checkCaseConsistency env' cases'
    -- check postconditions
    ensures <- map fst <$> traverse (checkExpr env''' U TBoolean) posts
    -- Note: invariants are ignored for the time being and not checked
    pure $ let constr = Constructor p cid (Interface p' params) payable block' ensures []
               env'''' = addConstructor cid constr env'''
           in (constr, clearLocalEnv env'''', cnstr1)

balanaceDeclared :: Pn -> Map Id (ValueType, Integer) -> Err ()
balanaceDeclared p storageTyping =
    case Map.lookup "BALANCE" storageTyping of
      Just _ -> pure ()
      Nothing -> throw (p, "A payable constructor must explicitly declare BALANCE of type uint256")

-- | Extend a list of constraints with additional preconditions. Useful for adding integer bounds.
addIffs :: [Exp ABoolean Untimed] -> [Constraint Untimed] -> [Constraint Untimed]
addIffs iffs cnstrs = addIff <$> cnstrs
  where
    addIff :: Constraint Untimed -> Constraint Untimed
    addIff (BoolCnstr p src msg env e) =
        BoolCnstr p src msg (addPreconds iffs env) e
    addIff (CallCnstr p src msg env args cv cid) =
        CallCnstr p src msg (addPreconds iffs env) args cv cid

-- | Check that constructor/transition parameters have valid types
checkParams :: Env -> U.Arg -> Err ()
checkParams Env{storage} (Arg (ContractArg p c) _) =
  case Map.lookup c storage of
    Nothing -> throw (p, "Contract " <> c <> " is not a valid contract type")
    Just _ -> pure ()
-- TODO check that abi types are valid
checkParams _ _ = pure ()

{-
-- | Type check each case of a constructor
checkConstrCases :: Env -> [U.Case U.Creates]
                 -> Err (Map Id (ValueType, Integer), Cases [StorageUpdate], [Constraint Untimed])
checkConstrCases env cases = do
  -- check each case separately
  checkCases cases `bindValidation` \(cases', cnstr) -> do
    -- then construct storage typing and check consistency
    storageTyping <- checkStorageTyping cases
    pure (storageTyping, cases', cnstr)
  where
    checkCases :: [U.Case U.Creates] -> Err (Cases [StorageUpdate], [Constraint Untimed])
    checkCases [] = pure ([], [])
    checkCases ((U.Case p cond assigns):cs) =
        -- check case condition
        checkExpr env U TBoolean cond `bindValidation` \(ccond, cnstr1) -> do
        -- add case condition to environment preconditions
        let env' = addPreconds [ccond] env
        -- check assignments
        r2 <- unzip <$> traverse (checkAssign env') assigns
        -- check remaining cases
        r3 <- checkCases cs
        -- because we use applicative-do we need to do deconstruct tuples inside pure
        pure $ let (cases', cnstr3) = r3 in
               let (updates, cnstr2) = r2 in
               ((Case p ccond updates):cases', cnstr1 ++ concat cnstr2 ++ cnstr3)

    -- check that storage typing is consistent across all cases
    checkStorageTyping :: [U.Case U.Creates] -> Err (Map Id (ValueType, Integer))
    checkStorageTyping [] = pure mempty
    checkStorageTyping ((U.Case _ _ assigns):_) = do
        let typing = makeStorageTyping assigns 0
        consistentStorageTyping typing cases
        balanceConsistent assigns
        noDuplicates assigns
        noReserved assigns
        pure $ Map.fromList typing

    -- check that there are no duplicate storage variables in a case
    noDuplicates :: [U.Assign] -> Err ()
    noDuplicates [] = pure ()
    noDuplicates ((U.StorageVar p _ name, _):rest) =
        (assert (p, "Duplicate storage variable " <> show name) (not (any (\(U.StorageVar _ _ n, _) -> n == name) rest))) *>
        noDuplicates rest

    -- check that the storage variables do not conflict with i.e. names in the Rocq backend
    noReserved :: [U.Assign] -> Err ()
    noReserved [] = pure ()
    noReserved ((U.StorageVar p _ name, _):rest) =
        (assert (p, "Identifier " <> show name <> "is reserved") (notElem name reservedIds)) *>
        noReserved rest

    reservedIds = ["This", "Caller", "Origin", "Callvalue", "Timestamp", "Env", "State", "addr"]

    -- check that BALANCE is always declared with the right type
    balanceConsistent :: [U.Assign] -> Err ()
    balanceConsistent [] = pure ()
    balanceConsistent ((U.StorageVar p (ValueType typ) name, _):rest) =
        if name == "BALANCE" then
            (assert (p, "BALANCE must be of type uint256") (eqS'' typ (TInteger 256 Unsigned))) *>
            balanceConsistent rest
        else
            balanceConsistent rest

    -- make the storage typing from a list of assignments
    makeStorageTyping :: [U.Assign] -> Integer ->  [(Id, (ValueType, Integer))]
    makeStorageTyping [] _ = []
    makeStorageTyping ((U.StorageVar _ typ name, _):rest) slot =
        -- The BALANCE field is a special case as it is not a storage variable so we map it to slot -1
        if name == "BALANCE" then (name, (typ, -1)):makeStorageTyping rest slot
        else (name, (typ, slot)):makeStorageTyping rest (slot + 1)

    -- check that the storage typing is the same across all cases
    consistentStorageTyping :: [(Id, (ValueType, Integer))] -> [U.Case U.Creates] -> Err ()
    consistentStorageTyping _ [] = pure ()
    consistentStorageTyping typing ((U.Case p _ assigns):cases') =
        let typing' = makeStorageTyping assigns 0 in
        consistentStorageTyping typing cases' *>
        assert (p, "Inconsistent storage typing in constructor cases") (typing == typing')
-}

-- | Type check a list of transitions
checkBehaviours :: Env -> [U.Transition] -> Err ([Behaviour], Env, [Constraint Untimed])
checkBehaviours env [] = pure ([], env, [])
checkBehaviours env (b:bhs) =
    -- check that there are no duplicate transition names
    checkBehvName b bhs *>
    -- check individual transition
    checkBehaviour env b `bindValidation` \(tbehv, env', bcnstrs) -> do
      -- check remaining transitions
      bs' <- checkBehaviours env' bhs
      pure $ let (tbs, env'', bscnstrs) = bs' in
              (tbehv:tbs, env'', bcnstrs ++ bscnstrs)
    where
        checkBehvName :: U.Transition -> [U.Transition] -> Err ()
        checkBehvName (U.Transition pn name _ _ _ _ _) bhs' =
            case find (\(U.Transition _ n _ _ _ _ _) -> n == name) bhs' of
                Just _ -> throw (pn, "Transition " <> name <> "for contract " <> contract env <> " is already defined")
                Nothing -> if (name == contract env) then throw (pn, "Transition cannot have the same name as contract") else pure ()

-- | Type check a single transition
checkBehaviour :: Env -> U.Transition -> Err (Behaviour, Env, [Constraint Untimed])
checkBehaviour env@Env{contract} (U.Transition p name iface@(Interface _ params) payable rettype (U.Block iffs cases) posts) =
    -- add parameters to environment
    let env' = addCalldata params env in
    -- check that parameter types are valid
    traverse_ (checkParams env) params *>
    -- check preconditions
    -- checkPreconditions env' (addCallvalueZeroPrecond payable iffs) `bindValidation` \(iffs', cnstr1, env'') -> do
    checkBlock env' (argToValueType <$> rettype) (U.Block (addCallvalueZeroPrecond payable iffs) cases) `bindValidation` \(block', cnstr1, env'') -> do
    -- check postconditions
    ensures <- map fst <$> traverse (checkExpr env'' T TBoolean) posts
    -- return the behaviour
    pure $ let trans = Behaviour p name contract iface payable block' ensures
               env''' = addTransition contract trans env''
           in  (trans, env''', cnstr1)

adjustCasePosn :: Pn -> [U.Case] -> [U.Case]
adjustCasePosn p cases =
    map adjust cases
  where
    adjust (U.Case p' cond updates) = U.Case (if p' == nowhere then p else p') cond updates

checkBlock :: Env -> Maybe ValueType -> U.Block -> Err (Block Untimed, [Constraint Untimed], Env)
checkBlock env rettype (U.Block iff cases) =
  -- check preconditions
  checkPreconditions env iff `bindValidation` \(iffs', cnstr1, env') -> do
  -- check cases
  casesc <- unzip <$> traverse (checkCase env' rettype) cases -- TODO: why was this here? (adjustCasePosn p cases)
  pure $ let (cases', cnstrs2) = casesc
             -- check case consistency
             casecnstrs = checkCaseConsistency env' cases'
         in  (Block iffs' cases', cnstr1 ++ concat cnstrs2 ++ casecnstrs, env')

-- | Type check a single case of a behaviour
checkCase :: Env -> Maybe ValueType -> U.Case
              -> Err (Case, [Constraint Untimed])
checkCase env rettype (U.Case p cond effects) =
    -- check case condition
    checkExpr env U TBoolean cond `bindValidation` \(cond', cnstr1) ->
    -- add case condition to environment preconditions
    let env' = addPreconds [cond'] env in
    -- check effects
    checkEffects env' rettype effects `bindValidation` \(effects', cnstr2) -> do
    pure (Case p cond' effects', cnstr1 ++ cnstr2)

checkEffects :: Env -> Maybe ValueType -> U.Effects -> Err (Effects Untimed, [Constraint Untimed])
checkEffects env rettype (U.LocalOnly updates mret) =
    (unzip <$> traverse (checkStorageUpdate env) updates) `bindValidation` \(tupdates, cnstr1) -> do
    res <- case (rettype, mret) of
        (Nothing, Nothing) -> pure Nothing
        (Just (ValueType t), Just e)  ->  Just . first (TExp t) <$> checkExpr env U t e
        (Nothing, Just _)  -> throw (nowhere, "Transition does not return a value, but return expression provided") -- TODO: add position here somehow..
        (Just _, Nothing)  -> throw (nowhere, "Transition must return a value, but no return expression provided")
    checkOrderedUpdates tupdates
    pure $ let (mret', cnstr2) = case res of
                  Just (e, cs) -> (Just e, cs)
                  Nothing -> (Nothing, [])
            in (LocalOnly tupdates mret', concat cnstr1 ++ cnstr2)
checkEffects env rettype (U.LocalAndInteraction updates interaction block') = do 
    (unzip <$> traverse (checkStorageUpdate env) updates) `bindValidation` \(tupdates, cnstr1) -> do
      checkOrderedUpdates tupdates *>
        checkInteraction env interaction `bindValidation` \(interaction', cnstr2, env'') -> do
        checkBlock (incrInters env'') rettype block' `bindValidation` \(block'', cnstr3, env''') -> do
          pure (LocalAndInteraction tupdates interaction' block'', concat cnstr1 ++ cnstr2 ++ cnstr3)

checkInteraction :: Env -> U.Interaction -> Err (Interaction Untimed, [Constraint Untimed], Env)
checkInteraction env (U.CallI p static calle fun args callvalue success retVars) =
  inferExpr env U calle `bindValidation` \(TExp t calle', cnstr0) ->
    case t of
      TContract c -> do
        case Map.lookup c (transitions env) of
          Just transs -> 
            case Map.lookup fun transs of
              Just trans -> do
                let sig = map (\(Arg typ _) -> typ) (case _interface trans of Interface _ as -> as)
                    payable = _isPayable trans
                cvc <- case (payable, callvalue) of
                        (NonPayable, Just _) -> throw (p, "Constructor " <> show c <> " is not payable, but call value provided")
                        (Payable, Just cvExpr) -> first Just <$> checkExpr env U (TInteger 256 Unsigned) cvExpr
                        (NonPayable, Nothing)    -> pure (Nothing, [])
                        (Payable, Nothing)     -> pure (Just $ LitInt nowhere 0, [])
                argsc <- checkArgs env U p (argToValueType <$> sig) args
                env' <- addRetdata retArgs env -- TODO: Maybe more specific contract type..
                pure $ let (args', cnstr1) = argsc
                           (cv, cnstr2) = cvc
                           callcnstr = CallCnstr p "" msg env args' cv c
                           msg = "Preconditions of transition call to " <> show c <> " are not guaranteed to hold"
                           -- env' = 
                        in (TypedCallI p static calle' fun args' cv success retVars, callcnstr : cnstr0 ++ cnstr1 ++ cnstr2 , env')
              Nothing -> throw (p, "Contract " <> c <> "does not implement " <> fun)
          Nothing -> throw (p, "Unknown contract type: " <> c)
      TAddress -> do
        -- TODO: think about actual typechecking of arguments here..
        cvc <- case callvalue of
                Just cvExpr -> first Just <$> checkExpr env U (TInteger 256 Unsigned) cvExpr
                Nothing     -> pure (Just $ LitInt nowhere 0, [])
        argsc <- traverse (inferExpr env U) args
        env' <- addRetdata retArgs env -- TODO: Maybe more specific contract type..
        pure $ let (args', cnstr1) = unzip argsc
                   (cv, cnstr2) = cvc
                   -- callcnstr = CallCnstr p "" msg env args' cv c
                   -- msg = "Preconditions of transition call to " <> show c <> " are not guaranteed to hold"
                   -- env' = 
                in (UntypedCallI p static calle' fun args' cv success retVars, cnstr0 ++ concat cnstr1 ++ cnstr2 , env')
      _ -> throw (p, "Call with non-callable type: " <> prettyTValueType t)
  where retArgs = (Arg (AbiArg AbiBoolType) success) : maybe [] (\(Interface _ decls) -> decls) retVars
checkInteraction env@Env{constructors}  (U.CreateI p retVar c args callvalue) =
  case Map.lookup c constructors of
    Just cnstr -> do
        let sig = map (\(Arg typ _) -> typ) (case _cinterface cnstr of Interface _ as -> as)
            payable = _cisPayable cnstr
        cvc <- case (payable, callvalue) of
                (NonPayable, Just _) -> throw (p, "Constructor " <> show c <> " is not payable, but call value provided")
                (Payable, Just cvExpr) -> first Just <$> checkExpr env U (TInteger 256 Unsigned) cvExpr
                (NonPayable, Nothing)    -> pure (Nothing, [])
                (Payable, Nothing)     -> pure (Just $ LitInt nowhere 0, [])
        argsc <- checkArgs env U p (argToValueType <$> sig) args
        env' <- addRetdata [(Arg (AbiArg AbiAddressType) retVar)] env -- TODO: Maybe more specific contract type..
        pure $ let (args', cnstr1) = argsc
                   (cv, cnstr2) = cvc
                   callcnstr = CallCnstr p "" msg env args' cv c
                   msg = "Preconditions of constructor call to " <> show c <> " are not guaranteed to hold"
                   -- env' = 
                in (CreateI p retVar c args' cv, callcnstr : cnstr1 ++ cnstr2, env')
    Nothing -> throw (p, "Unknown constructor " <> show c)

checkOrderedUpdates :: [StorageUpdate] -> Err ()
checkOrderedUpdates [] = pure ()
checkOrderedUpdates ((Update _ ref _):upds) =
  (assert (orderErr $ posnFromRef ref) $ not (any (leRef ref . getRef) upds)) *>
  checkOrderedUpdates upds
  where

    orderErr p' = (p', "Storage updates must be listed from the least to most specific")

    getRef :: StorageUpdate -> Ref LHS Untimed
    getRef (Update _ ref' _) = ref'

    leRef :: Ref LHS Untimed -> Ref LHS Untimed -> Bool
    leRef r1 r2 = r1 == r2 || ltRef r1 r2

    ltRef :: Ref LHS Untimed -> Ref LHS Untimed -> Bool
    ltRef (RField _ r1 _ _) r2 = r1 == r2 || ltRef r1 r2
    ltRef (RArrIdx _ r1 _ _ ) r2 = r1 == r2 || ltRef r1 r2
    ltRef (SVar _ _ _ _ _) _ = False


-- | Check a list of preconditions one by one and add each one of them to the environment before checking the next
checkPreconditions :: Env -> [U.Expr] -> Err ([Exp ABoolean Untimed], [Constraint Untimed], Env)
checkPreconditions env [] = pure ([], [], env)
checkPreconditions env (pre:pres) =
    -- check the precondition
    checkExpr env U TBoolean pre `bindValidation` \(pre', cnstr1) ->
    -- add precondition to environment
    let env' = addPreconds [pre'] env in
    -- check remaining preconditions
    (checkPreconditions env' pres) `bindValidation` \(pres', cnstr2, env'') ->
    pure (pre':pres', cnstr1 ++ cnstr2, env'')

-- | Create all combinations of pairs from a list
combine :: [a] -> [(a,a)]
combine lst = combine' lst []
  where
    combine' [] acc = concat acc
    combine' (x:xs) acc =
      let xcomb = [ (x, y) | y <- xs] in
      combine' xs (xcomb:acc)

-- | Create a disjunction (logical OR) of a list of boolean expressions
mkOr :: [Exp ABoolean t] -> Exp ABoolean t
mkOr [] = LitBool nowhere False
mkOr (c:cs) = foldr (Or nowhere) c cs

-- | Create a conjunction (logical AND) of a list of boolean expressions
mkAnd :: [Exp ABoolean t] -> Exp ABoolean t
mkAnd [] = LitBool nowhere True
mkAnd (c:cs) = foldr (And nowhere) c cs


-- | Check if a constructor/transition is non-payable and if so generate a precondition
-- to ensure that it is not called with value.
addCallvalueZeroPrecond :: IsPayable -> [U.Expr] -> [U.Expr]
addCallvalueZeroPrecond Payable iffs = iffs
addCallvalueZeroPrecond NonPayable iffs =
    (U.EEq nowhere (U.EnvExp nowhere Callvalue) (U.IntLit nowhere 0)) : iffs


{-
-- | Implicitly add uint256 BALANCE if the constructor is not payable
initBalance :: IsPayable -> [U.Case U.Creates] -> [U.Case U.Creates]
initBalance Payable cases = cases
initBalance NonPayable cases =
    map addBalanceToCase cases
  where
    -- Add balance to list of assignments
    addBalanceToCase :: U.Case U.Creates -> U.Case U.Creates
    addBalanceToCase (U.Case p cond updates) =
        if hasBalanceField updates then
            U.Case p cond updates
        else
          let balanceUpdate = (U.StorageVar p (ValueType (TInteger 256 Unsigned)) "BALANCE", U.IntLit nowhere 0) in
          U.Case p cond (balanceUpdate : updates)

    -- Check if BALANCE is already there
    hasBalanceField :: [U.Assign] -> Bool
    hasBalanceField [] = False
    hasBalanceField ((U.StorageVar _ _ name, _):rest) =
        (name == "BALANCE") || hasBalanceField rest
        -}

-- | Check that case conditions in a case block are mutually exclusive and exhaustive
checkCaseConsistency :: Env -> [Case] -> [Constraint Untimed]
checkCaseConsistency env cases =
    [ BoolCnstr getCasePos "" "Cases are not mutually exclusive" env (mkNonoverlapAssertion conds)
    , BoolCnstr getCasePos "" "Cases are not exhaustive" env (mkExhaustiveAssertion conds)
    ]
    where
        getCasePos = case cases of
            [] -> nowhere
            (Case p _ _ : _) -> p

        conds :: [Exp ABoolean Untimed]
        conds = map (\(Case _ cond _) -> cond) cases
        -- For every pair of case conditions we assert that they are true
        -- simultaneously. The query must be unsat.
        mkNonoverlapAssertion :: [Exp ABoolean Untimed] -> Exp ABoolean Untimed
        mkNonoverlapAssertion caseconds =
            mkAnd $ (\(c1, c2) -> Neg nowhere (And nowhere c1 c2)) <$> combine caseconds

        mkExhaustiveAssertion :: [Exp ABoolean Untimed] -> Exp ABoolean Untimed
        mkExhaustiveAssertion caseconds = mkOr caseconds

{-
-- | Type check initial storage assignment
checkAssign :: Env -> U.Assign -> Err (StorageUpdate, [Constraint Untimed])
checkAssign env (U.StorageVar p (ValueType typ) var, expr) = do
    -- check that the type is a valid slot type
    validSlotType env p typ
    -- check the RHS expression
    (expr', cnstr) <- checkExpr env U typ expr
    pure (Update typ (SVar p Neither (contract env) (var)) expr', cnstr)
-}

-- | Check that a type is a valid slot type
validSlotType :: Env -> Pn -> TValueType a -> Err ()
validSlotType _ p (TInteger size _) =
    unless (size `elem` [i*8 | i <- [1..32]]) $
    throw (p, "Invalid integer size: " <> show size)
validSlotType env p (TContract c) =
    case Map.lookup c (storage env) of
    Just _ -> pure ()
    Nothing -> throw (p, "Contract " <> c <> " is not a valid contract type")
validSlotType _ _  TAddress = pure ()
validSlotType _ _ TBoolean = pure ()
validSlotType _ _ TByteStr = pure ()
validSlotType _ p (TStruct _) = throw (p, "Struct types are not supported yet")
validSlotType _ _ TUnboundedInt = pure ()
validSlotType env p (TArray _ elemtyp) = validSlotType env p elemtyp
validSlotType env p (TMapping (ValueType keytyp) (ValueType val)) =
    assert (p, "Mapping key type must be a base type") (validKeyType keytyp) *>
    assert (p, "Mapping value type cannot be a contract") (validValueType val) *>
    validSlotType env p keytyp *>
    validSlotType env p val
  where
    validKeyType ::  TValueType a -> Bool
    validKeyType TInteger{} = True
    validKeyType TAddress = True
    validKeyType TBoolean = True
    validKeyType TByteStr = True
    validKeyType TUnboundedInt = True
    validKeyType _ = False

    validValueType :: TValueType a -> Bool
    validValueType TContract{} = False
    validValueType TInteger{} = True
    validValueType TAddress = True
    validValueType TBoolean = True
    validValueType TByteStr = True
    validValueType TUnboundedInt = True
    validValueType TArray{} = True
    validValueType TMapping{} = True
    validValueType (TStruct {}) = False


-- | Type check a storage update in a transition case
checkStorageUpdate :: Env -> U.StorageUpdate -> Err (StorageUpdate, [Constraint Untimed])
checkStorageUpdate env (U.Update ref expr) =
    checkRef env SLHS U ref `bindValidation` \(ValueType typ, tref, cnstr) ->
    checkExpr env U typ expr `bindValidation` \(expr', cnstr') ->
    pure (Update typ tref expr', cnstr ++ cnstr')

-- | Type check a variable reference
checkRef :: forall t k. Env -> SRefKind k -> Mode t -> U.Ref -> Err (ValueType, Ref k t, [Constraint t])
-- Single variable reference
checkRef Env{contract, calldata, storage, numInters} kind mode (U.RVar p resettag tag name) =
    let resettag' = fromMaybe numInters resettag in
    case Map.lookup name calldata of
      -- calldata variable
      Just typ -> case kind of
        SLHS -> throw (p, "Cannot use calldata variable " <> show name <> " as LHS reference")
        SRHS -> pure (argToValueType typ, CVar p typ name, [])
      Nothing -> case Map.lookup contract storage of
        -- storage variable
        Just storageTyping -> case Map.lookup name storageTyping of
            Just (typ, _) ->
                case (tag, mode) of
                    (U.Neither, U) -> pure (typ, SVar p Neither resettag' contract name, [])
                    (U.Pre, T)     -> pure (typ, SVar p Pre resettag' contract name, [])
                    (U.Post, T)    -> pure (typ, SVar p Post resettag' contract name, [])
                    _              -> throw (p, "Mismatched timing for storage variable " <> show name <> ": declared " <> show tag <> ", used in " <> show mode)
            Nothing -> throw (p, "Unbound variable " <> show name)
        Nothing -> throw (p, "Unbound variable " <> show name) -- accessing storage variable not yet created
-- Indexed reference (mapping or array)
checkRef env kind mode (U.RIndex p en idx) =
  -- check base reference
  checkRef env kind mode en `bindValidation` \(ValueType styp, ref :: Ref k t, cnstr) ->
  case styp of
    TArray len typ@VType ->
        -- check index and add array bound constraint
        checkExpr env mode defaultUInteger idx `bindValidation` \(idx', cnstr') ->
        let arrmsg = "Array index in not guaranteed to be in range for array of length " <> show len in
        pure (ValueType typ, RArrIdx p ref idx' len, makeArrayBoundConstraint p arrmsg env len idx':cnstr ++ cnstr')
    TMapping (ValueType keytyp) (ValueType valtyp) ->
        -- check index
        checkExpr env mode keytyp idx `bindValidation` \(ix, cnstr') ->
        case kind of
            SLHS -> throw (p, "Cannot use mapping indexing as LHS reference")
            SRHS -> pure (ValueType valtyp, RMapIdx p (TRef styp kind ref) (TExp keytyp ix), cnstr ++ cnstr')
    _ -> throw (p, "An indexed reference should have an array or mapping type" <> show en)
-- Storage field access
checkRef env kind mode (U.RField p en x) =
  -- check base reference
  checkRef env kind mode en `bindValidation` \(ValueType styp, ref :: Ref k t, cnstr) ->
  -- look up field type
  case styp of
    TContract c -> case Map.lookup c (storage env) of
      Just cenv -> case Map.lookup x cenv of
        Just (typ, _) ->  pure (typ, RField p ref c x, cnstr)
        Nothing -> throw (p, "Contract " <> c <> " does not have field " <> x)
      Nothing -> error $ "Internal error: Invalid contract type " <> show c
    _ -> throw (p, "Reference should have a contract type" <> show en)

-- | If an `inrange e` predicate appears in the source code, then the inrange
-- predicate is propagated to all subexpressions of `e`.
genInRange :: TValueType AInteger -> Exp AInteger t -> [Exp ABoolean t]
genInRange t e@(LitInt _ _) = [InRange nowhere t e]
genInRange t e@(VarRef _ _ _)  = [InRange nowhere t e]
genInRange t e@(Add _ e1 e2) = [InRange nowhere t e] <> genInRange t e1 <> genInRange t e2
genInRange t e@(Sub _ e1 e2) = [InRange nowhere t e] <> genInRange t e1 <> genInRange t e2
genInRange t e@(Mul _ e1 e2) = [InRange nowhere t e] <> genInRange t e1 <> genInRange t e2
genInRange t e@(Div _ e1 e2) = [InRange nowhere t e] <> genInRange t e1 <> genInRange t e2
genInRange t e@(Mod _ e1 e2) = [InRange nowhere t e] <> genInRange t e1 <> genInRange t e2
genInRange t e@(Exp _ e1 e2) = [InRange nowhere t e] <> genInRange t e1 <> genInRange t e2
genInRange t e@(IntEnv _ _) = [InRange nowhere t e]
genInRange t e@(Address _ _ _) = [InRange nowhere t e]
genInRange _ (IntMin _ _)  = error "Internal error: invalid range expression"
genInRange _ (IntMax _ _)  = error "Internal error: invalid range expression"
genInRange _ (UIntMin _ _) = error "Internal error: invalid range expression"
genInRange _ (UIntMax _ _) = error "Internal error: invalid range expression"
genInRange _ (ITE _ _ _ _) = error "Internal error: invalid range expression"


typeMismatchErr :: forall a b res. Pn -> TValueType a -> TValueType b -> Err res
typeMismatchErr p t1 t2 = throw (p, "Type " <> prettyTValueType t1 <> " should match type " <> prettyTValueType t2)

relaxedValueEquality :: ValueType -> ValueType -> Bool
relaxedValueEquality (ValueType t1) (ValueType t2) = isJust $ relaxedtestEquality t1 t2

-- | Relaxed equality for types that considers all integer types as equal
relaxedtestEquality :: TValueType a -> TValueType b -> Maybe (a :~: b)
relaxedtestEquality (TInteger _ _) (TInteger _ _) = Just Refl
relaxedtestEquality (TInteger _ _) TUnboundedInt = Just Refl
relaxedtestEquality TUnboundedInt (TInteger _ _) = Just Refl
relaxedtestEquality TUnboundedInt TUnboundedInt = Just Refl
relaxedtestEquality (TArray n1 t1) (TArray n2 t2) | n1 == n2 = relaxedtestEquality t1 t2 >>= \Refl -> Just Refl
relaxedtestEquality (TMapping k1 t1) (TMapping k2 t2) =
  if relaxedValueEquality k1 k2 && relaxedValueEquality t1 t2 then Just Refl else Nothing
relaxedtestEquality (TStruct fs1) (TStruct fs2) =
  if all (uncurry relaxedValueEquality) (zip fs1 fs2) then Just Refl else Nothing
relaxedtestEquality t1 t2 = testEquality t1 t2

relaxedIntCheck :: TValueType a -> TValueType b -> Bool
relaxedIntCheck t1 t2 = isJust $ relaxedtestEquality t1 t2

-- | Check that two types are equal under the relaxed equality
checkEqType :: forall a b. Pn -> TValueType a -> TValueType b -> Err ()
checkEqType p t1 t2 =
    if relaxedIntCheck t1 t2 then pure ()
    else typeMismatchErr p t1 t2

-- | Combines two types that should satisfy the relaxedtestEquality to the most general type
combineTypes :: TValueType a -> TValueType a -> TValueType a
combineTypes (TInteger w1 Signed) (TInteger w2 Signed) = TInteger (max w1 w2) Signed
combineTypes (TInteger w1 Unsigned) (TInteger w2 Unsigned) = TInteger (max w1 w2) Unsigned
combineTypes (TInteger w1 Signed) (TInteger w2 Unsigned) =
    if w1 > w2 then TInteger w1 Signed
    else TUnboundedInt
combineTypes (TInteger w1 Unsigned) (TInteger w2 Signed) =
    if w2 > w1 then TInteger w2 Signed
    else TUnboundedInt
combineTypes TUnboundedInt TUnboundedInt = TUnboundedInt
combineTypes (TInteger _ _) TUnboundedInt = TUnboundedInt
combineTypes TUnboundedInt (TInteger _ _) = TUnboundedInt
combineTypes (TArray n1 t1') (TArray _ t2') =
    let c = combineTypes t1' t2'
    in TArray n1 c
combineTypes t1 _ = t1

fitsIn :: TValueType AInteger -> TValueType AInteger -> Bool
fitsIn (TInteger w1 s1) (TInteger w2 s2)
  | s1 == s2 = w1 <= w2
  | s1 == Unsigned && s2 == Signed = w1 < w2
  | otherwise = False
fitsIn TUnboundedInt (TInteger _ _) = False
fitsIn (TInteger _ _) TUnboundedInt = True
fitsIn TUnboundedInt TUnboundedInt = True
fitsIn TAddress _ = False -- for now do not coerce address to integer
fitsIn _ TAddress = False

-- | Check if the given expression can be typed with the given type
checkExpr :: forall t a. Env -> Mode t -> TValueType a -> U.Expr -> Err (Exp a t, [Constraint t])
-- Mapping Expressions
checkExpr env mode mtyp@(TMapping (ValueType keytyp) (ValueType valtyp)) (U.MappingUpd p ref mapexp) = do
    checkRef env SRHS mode ref `bindValidation` \(ValueType rtyp, tref, cnstr1) -> do
        checkEqType p mtyp rtyp
        updsc <- unzip <$> traverse (\(k,v) -> do
            kc <- checkExpr env mode keytyp k
            vc <- checkExpr env mode valtyp v
            pure $ let (k', cnstr2) = kc
                       (v', cnstr3) = vc
                   in ((k', v'), cnstr2 ++ cnstr3)) mapexp
        pure $ let (updates, cnstr2) = updsc in
           (MappingUpd p tref keytyp valtyp updates, cnstr1 ++ concat cnstr2)
checkExpr env mode (TMapping (ValueType keytyp) (ValueType valtyp)) (U.Mapping p mapexp) = do
    mapc <- unzip <$> traverse (\(k,v) -> do
        kc <- checkExpr env mode keytyp k
        vc <- checkExpr env mode valtyp v
        pure $ let (k', cnstr2) = kc
                   (v', cnstr3) = vc
                in ((k', v'), cnstr2 ++ cnstr3)) mapexp
    pure $ let (map', cnstr1) = mapc in
           (Mapping p keytyp valtyp map', concat cnstr1)
-- Integer Expressions
checkExpr env mode t1@(TInteger _ _) e =
    inferExpr env mode e `bindValidation` \(TExp t2 te, cs) -> do
    let msg = "Integer expression of type " <> prettyTValueType t2 <> " is not guaranteed to fit in type " <> prettyTValueType t1
    case t2 of
        (TInteger _ _) ->
            if t2 `fitsIn` t1 then pure (te, cs)
            else pure (te, cs ++ [makeIntegerBoundConstraint (getPosn e) msg env t1 te])
        TUnboundedInt -> pure (te, cs ++ [makeIntegerBoundConstraint (getPosn e) msg env t1 te])
        _ -> typeMismatchErr (getPosn e) t1 t2
checkExpr env mode t1@TUnboundedInt e =
    inferExpr env mode e `bindValidation` \(TExp t2 te, cs) -> do
    case t2 of
        (TInteger _ _) -> pure (te, cs)
        TUnboundedInt -> pure (te, cs)
        _ -> typeMismatchErr (getPosn e) t1 t2
-- Array Expressions
-- TODO: all array cases except the last are probably not necessary, but may lead to nicer error messages,
-- less unnecessary constraints and prettier code.
-- The idea is that we can avoid unnecessary constraint enforcement by using `checkExpr` on each element
-- If, instead, the whole array is inferred first, we are then forced to apply
checkExpr env mode (TArray len t) (U.EArray p es) = do
    if len == length es then pure () else typeMismatchErr p (TArray len t) (TArray (length es) t)
    r <- unzip <$> traverse (checkExpr env mode t) es
    pure $ let (tes,cs) = r in (Array p tes, concat cs)
checkExpr _ _ t (U.EArray p _) = throw (p, "Array expression cannot have type " <> prettyTValueType t)
checkExpr env mode t@(TArray _ _) (U.EITE p e1 e2 e3) = do
    r1 <- checkExpr env mode TBoolean e1
    r2 <- checkExpr env mode t e2
    r3 <- checkExpr env mode t e3
    pure $ let (te1, cnstr1) = r1
               (te2, cnstr2) = r2
               (te3, cnstr3) = r3
           in (ITE p te1 te2 te3, cnstr1 ++ cnstr2 ++ cnstr3)
checkExpr env mode t1@(TArray _ _) e =
    -- Question: do we allow array subtyping here?
    let pn = getPosn e in
    inferExpr env mode e `bindValidation` \(TExp t2 te, cs) ->
        flip (maybe (typeMismatchErr pn t1 t2)) (relaxedtestEquality t1 t2) $ \Refl ->
            case (fst $ flattenValueType t1, fst $ flattenValueType t2) of
                (bt1@(TInteger _ _), bt2@(TInteger _ _)) ->
                    let msg = "Array element integer expression of type " <> prettyTValueType bt2 <> " is not guaranteed to fit in type " <> prettyTValueType bt1 in
                    if bt2 `fitsIn` bt1 then pure (te, cs)
                    else pure (te, cs ++ (makeIntegerBoundConstraint (getPosn e) msg env bt1 <$> expandArrayExpr t2 te))
                (bt1@(TInteger _ _), TUnboundedInt) ->
                    let msg = "Array element integer expression of type " <> prettyTValueType TUnboundedInt <> " is not guaranteed to fit in type " <> prettyTValueType bt1 in
                    pure (te, cs ++ (makeIntegerBoundConstraint (getPosn e) msg env bt1 <$> expandArrayExpr t2 te))
                (TUnboundedInt, TInteger _ _) -> pure (te, cs)
                (TUnboundedInt, TUnboundedInt) -> pure (te, cs)
                _ -> typeMismatchErr (getPosn e) t1 t2
checkExpr env mode t1 e =
    let pn = getPosn e in
    inferExpr env mode e `bindValidation` \(TExp t2 te, cs) ->
    maybe (typeMismatchErr pn t1 t2) (\Refl -> pure (te, cs)) $ testEquality t1 t2


-- | Attempt to infer a type of an expression. If successful, it returns an
-- existential package of the infered typed together with the typed expression.
inferExpr :: forall t. Env -> Mode t -> U.Expr -> Err (TypedExp t, [Constraint t])
inferExpr env@Env{constructors} mode e = case e of
  -- Some known constants. TODO: add a more principled constant propagation pass before typing.
  U.ESub _ (U.EExp p (U.IntLit _ 2) (U.IntLit _ 256)) (U.IntLit _ 1) ->
    pure (TExp (TInteger 256 Unsigned) (UIntMax p 256), [])
  U.ESub _ (U.EExp p (U.IntLit _ 2) (U.IntLit _ 255)) (U.IntLit _ 1) ->
    pure (TExp (TInteger 256 Signed) (IntMax p 256), [])
  -- Boolean expressions
  U.ENot    p v1    -> first (wrapOp (Neg  p) TBoolean) <$> checkExpr env mode TBoolean v1
  U.EAnd    p v1 v2 -> boolOp2 (And  p) TBoolean v1 v2
  U.EOr     p v1 v2 -> boolOp2 (Or   p) TBoolean v1 v2
  U.EImpl   p v1 v2 -> boolOp2 (Impl p) TBoolean v1 v2
  U.ELT     p v1 v2 -> boolOp2 (LT   p) TUnboundedInt v1 v2
  U.ELEQ    p v1 v2 -> boolOp2 (LEQ  p) TUnboundedInt v1 v2
  U.EGEQ    p v1 v2 -> boolOp2 (GEQ  p) TUnboundedInt v1 v2
  U.EGT     p v1 v2 -> boolOp2 (GT   p) TUnboundedInt v1 v2
  U.EEq     p v1 v2 -> first (TExp TBoolean) <$> polyEqcheck p Eq v1 v2
  U.ENeq    p v1 v2 -> first (TExp TBoolean) <$> polyEqcheck p NEq v1 v2
  U.BoolLit p v1    -> pure (TExp TBoolean (LitBool p v1), [])
  U.EInRange _ (fromAbiType -> ValueType tr@(TInteger _ _)) v ->
    inferExpr env mode v `bindValidation` \(TExp t te, cnstr) ->
    case t of
      TInteger _ _ -> pure (TExp TBoolean . andExps $ genInRange tr te, cnstr)
      TUnboundedInt -> pure (TExp TBoolean . andExps $ genInRange tr te, cnstr)
      _ -> throw (getPosn e, "inRange can only be applied to integer expressions")
  U.EInRange _ _ _ -> throw (getPosn e, "inRange can be used only with integer types")
  U.EAdd   p v1 v2 -> arithOp2 (Add p) v1 v2
  U.ESub   p v1 v2 -> arithOp2 (Sub p) v1 v2
  U.EMul   p v1 v2 -> arithOp2 (Mul p) v1 v2
  U.EDiv   p v1 v2 -> arithOp2 (Div p) v1 v2
  U.EMod   p v1 v2 -> arithOp2 (Mod p) v1 v2
  U.EExp   p v1 v2 -> arithOp2 (Exp p) v1 v2
  U.IntLit p v1    ->
    pure (TExp (litBoundedType v1) (LitInt p v1), [])
   where
    -- Determine the narrowest integer type that can hold the given literal value
    litBoundedType :: Integer -> TValueType AInteger
    litBoundedType v | v >= 0 && v <= maxUnsigned 256 = TInteger (findBoundUnsigned v) Unsigned
                      | otherwise && v >= minSigned 256 && v <= maxSigned 256 = TInteger (findBoundSigned v) Signed
                      | otherwise = TUnboundedInt

  U.EArray p l -> (unzip <$> traverse (inferExpr env mode) l) `bindValidation` \(tes, cs) ->
    (, concat cs) <$> gatherElements tes
    where
      gatherElements :: [TypedExp t] -> Err (TypedExp t)
      gatherElements (TExp t1 te1:tes) =  TExp (TArray (length l) t1) <$> (Array p . (:) te1 <$> traverse (checkElement t1) tes)
      gatherElements [] = throw (p, "Internal error: Cannot infer type of empty array expression")

      -- TODO: this relies on combineTypes, look that over as it is not complete
      -- combinedType :: [TValueType a] -> Err (TValueType a)
      -- combinedType ts@(th : tl) = foldl' (\ta tb -> ta `bindValidation` combineTypes tb) (pure th) ts

      checkElement :: TValueType a -> TypedExp t -> Err (Exp a t)
      checkElement t (TExp t' te) = maybe (typeMismatchErr (posnFromExp te) t t') (\Refl -> pure te) $ relaxedtestEquality t t'

  -- Control
  U.EITE p e1 e2 e3 ->
    ((,,) <$> (checkExpr env mode TBoolean e1) <*> (inferExpr env mode e2) <*> (inferExpr env mode e3))
    `bindValidation` \((te1, cnstr1), (TExp t2 te2, cnstr2), (TExp t3 te3, cnstr3)) ->
      case relaxedtestEquality t2 t3 of
        Nothing   -> typeMismatchErr p t2 t3
        Just Refl -> pure (TExp (combineTypes t2 t3) (ITE p te1 te2 te3), cnstr1 ++ cnstr2 ++ cnstr3)
  -- Environment variables
  U.EnvExp p v -> pure (TExp (ethEnv v) (IntEnv p v), [])
  -- Variable references
  U.ERef ref ->
    (\(ValueType typ, tref, cnstr) -> (TExp typ (VarRef (getPosRef ref) typ tref), cnstr)) <$> checkRef env SRHS mode ref
  -- Address-of operator
  U.AddrOf _ (U.IntLit p' 0) -> pure (TExp TAddress (LitInt p' 0), []) -- address(0) becomes literal 0
  U.AddrOf p e1 -> do
    inferExpr env mode e1 `bindValidation` \(TExp ty e', cnstr) ->
      case ty of
        TContract c -> pure (TExp TAddress (Address p c e'), cnstr)
        _ -> throw (p, "Expression of type " <> show ty <> " cannot be converted to address")
  -- Mapping Epxressions
  U.Mapping p _ -> throw (p, "The type of mappings cannot be inferred.")
  U.MappingUpd p _ _ -> throw (p, "The type of mappings cannot be inferred.")
  _ -> undefined
  where
    wrapOp f t e1 = TExp t (f e1) -- use sign to let Haskell automatically derive the type here
    --wrapOp2 f t e1 e2 = TExp t (f e1 e2)

    boolOp2 :: forall a. (Exp a t -> Exp a t -> Exp ABoolean t) -> TValueType a -> U.Expr -> U.Expr -> Err (TypedExp t, [Constraint t])
    boolOp2 f t e1 e2 = do
      (\(e1', c1) (e2', c2) -> (TExp TBoolean (f e1' e2'), c1 ++ c2)) <$> checkExpr env mode t e1 <*> checkExpr env mode t e2
    arithOp2 :: (Exp AInteger t -> Exp AInteger t -> Exp AInteger t) -> U.Expr -> U.Expr -> Err (TypedExp t, [Constraint t])
    arithOp2 f e1 e2 = do
      -- Could generate more precise int type here
      (\(e1', c1) (e2', c2) -> (TExp TUnboundedInt (f e1' e2'), c1 ++ c2)) <$> checkExpr env mode TUnboundedInt e1 <*> checkExpr env mode TUnboundedInt e2
    polyEqcheck :: forall z. Pn -> (forall y. Pn -> TValueType y -> Exp y t -> Exp y t -> z) -> U.Expr -> U.Expr -> Err (z, [Constraint t])
    polyEqcheck pn cons e1 e2 = do
       ((,) <$> (inferExpr env mode e1) <*> (inferExpr env mode e2)) `bindValidation` \( (TExp t1 te1, c1), (TExp t2 te2, c2) ) ->
        eqType pn t1 *>
        eqType pn t1 *>
        case relaxedtestEquality t1 t2 of
          Nothing   -> typeMismatchErr pn t1 t2
          Just Refl -> pure (cons pn t1 te1 te2, c1 ++ c2)

    eqType :: Pn -> TValueType a -> Err ()
    eqType _ TInteger{} = pure ()
    eqType _ TUnboundedInt = pure ()
    eqType _ TBoolean = pure ()
    eqType _ TAddress = pure ()
    eqType pn (TContract _) = throw (pn, "Equality is not supported for contract types")
    eqType pn (TArray _ t) = eqType pn t -- TODO Do we want array equality? Act's semantics support it.
    eqType pn (TMapping _ _) = throw (pn, "Equality is not supported for mapping types")
    eqType pn (TStruct ts) = traverse_ (\(ValueType t) -> eqType pn t) ts
    eqType _ (TByteStr) = pure ()

-- | Type check a list of argument expressions against a list of expected types
checkArgs :: forall t. Env -> Mode t -> Pn -> [ValueType] -> [U.Expr] -> Err ([TypedExp t], [Constraint t])
checkArgs _ _ _ [] [] = pure ([], [])
checkArgs env mode pn (ValueType t:types) (e:exprs) =
    (\(e', cnstr1) (es', cnstr2) -> (TExp t e' : es', cnstr1 ++ cnstr2)) <$> checkExpr env mode t e <*> checkArgs env mode pn types exprs

checkArgs _ _ pn _ _ = throw (pn, "Argument length mismatch")

-- | Compute the maximum unsigned integer value for a given bit size
maxUnsigned :: Int -> Integer
maxUnsigned bits = 2 ^ bits - 1

-- | Compute the maximum signed integer values for a given bit size
maxSigned :: Int -> Integer
maxSigned bits = 2 ^ (bits - 1) - 1

-- | Compute the minimum signed integer value for a given bit size
minSigned :: Int -> Integer
minSigned bits = - (2 ^ (bits - 1))

-- | Find the smallest bit size that can hold the given signed integer value
findBoundSigned :: Integer -> Int
findBoundSigned v = go 8
  where
    go bits | v >= minSigned bits && v <= maxSigned bits = bits
            | bits >= 256 = 256
            | otherwise   = go (bits + 8)

-- | Find the smallest bit size that can hold the given unsigned integer value
findBoundUnsigned :: Integer -> Int
findBoundUnsigned v = go 8
  where
    go bits | 0 <= v && v <= maxUnsigned bits = bits
            | bits >= 256 = 256
            | otherwise   = go (bits + 8)

-- | Check if an integer expression can be represented as a 256-bit signed or unsigned integer
-- This is usefull for deciding whether to use signed or unsigned operations in hevm symbolic execution
-- Assumes that no overflows or underflows can occur in the expression
hasSign ::  IntSign -> Exp AInteger t -> Bool
hasSign s (LitInt _ n) = hasSignLit s n
hasSign s (Add _ e1 e2) = hasSign s e1 && hasSign s e2
hasSign s (Sub _ e1 e2) = hasSign s e1 && hasSign s e2
hasSign s (Mul _ e1 e2) = hasSign s e1 && hasSign s e2
hasSign s (Div _ e1 e2) = hasSign s e1 && hasSign s e2
hasSign s (Mod _ e1 e2) = hasSign s e1 && hasSign s e2
hasSign s (Exp _ e1 e2) = hasSign s e1 && hasSign s e2
hasSign _ (IntEnv _ _) = True
hasSign _ (Address _ _ _) = True
hasSign s (ITE _ _ e2 e3) = hasSign s e2 && hasSign s e3 -- ITE always results in signed integers
hasSign Signed (IntMin _ _) = True
hasSign Unsigned (IntMin _ _) = False
hasSign Signed (IntMax _ _) = True
hasSign Unsigned (IntMax _ _) = False
hasSign _ (UIntMin _ _) = True
hasSign _ (UIntMax _ _) = True
hasSign s (VarRef _ (TInteger _ s') _) = s == s'
hasSign _ (VarRef _ TUnboundedInt _) = False
hasSign _ (VarRef _ TAddress _) = True

-- | Check if a literal integer value can be represented as a 256-bit signed or unsigned integer
hasSignLit :: IntSign -> Integer -> Bool
hasSignLit Signed v = minSigned 256 <= v && v <= maxSigned 256
hasSignLit Unsigned v = 0 <= v && v <= maxUnsigned 256
